plugins {
    id 'java'
    id 'groovy'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.2'
}

repositories {
    jcenter()
}

if (!JavaVersion.current().java8Compatible) {
    throw new IllegalStateException('''A Haiku:
                                      |  This needs Java 8,
                                      |  You are using something else,
                                      |  Refresh. Try again.'''.stripMargin())
}

ext {
    vertxVersion = '3.0.0'
}

dependencies {
    compile 'io.vertx:vertx-web:' + vertxVersion
    compile 'io.vertx:vertx-lang-groovy:' + vertxVersion
}

shadowJar {
    classifier = 'fatty'
    manifest {
        attributes 'Main-Class': 'com.developerb.dd.DropwizardDashboardStarter'
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

run {
    args 'config'
}


task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}
